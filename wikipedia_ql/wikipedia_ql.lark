query: "from"i _S* source _S* nested_selectors

source: STRING

// TODO: Selector-sequence: foo bar? Or simplify to foo >> bar

nested_selectors: "{" _SM* selectors _SM* "}"

?selectors: selector _S* (";" _SM* selector)*
selector: _selector_expression as_named? _S* nested_selectors?

as_named: _S+ "as"i _S+ STRING

_selector_expression: css_selector | text_selector | sentence_selector | section_selector

text_selector: "text" ("[" _S* (STRING | REGEX) _S* "]")?
sentence_selector: "sentence" ("[" _S* (STRING | REGEX) _S* "]")?

section_selector: "section" ("[" _S* (STRING | REGEX) _S* "]" | "[" _S* "level" _S* "=" _S* INT _S* "]")*

// https://www.w3.org/TR/CSS21/grammar.html
css_selector: element_name (HASH | class | attrib | pseudo)* | (HASH | class | attrib | pseudo)+
element_name: IDENT | "*"
class: "." IDENT
attrib: "[" _S* IDENT _S* ( ( "=" | INCLUDES | DASHMATCH ) _S* ( IDENT | STRING ) _S* )? "]"
pseudo: ":" IDENT // ("(" _S* [IDENT _S*]? ")")? -- FIXME: Lark goes crazy on it...

FUNCTION: IDENT "("
INCLUDES: "~="
DASHMATCH: "|="
HASH: "#" NAME
IDENT:   "-"? NMSTART NMCHAR*
NAME:    NMCHAR+
NONASCII:  /[\240-\377]/
UNICODE:   /\\[0-9a-f]{1,6}(\r\n|[ \t\r\n\f])?/
ESCAPE:    UNICODE | /\\[^\r\n\f0-9a-f]/
NMSTART:   /[_a-z]/ | NONASCII | ESCAPE
NMCHAR:    /[_a-z0-9-]/ | NONASCII | ESCAPE

_S: (" "|/\t/)+
_SM: /[ \t\f\r\n]/+

REGEX: "/" /([^\/]|\\\/)+/ "/" "i"?

%import common.ESCAPED_STRING -> STRING
%import common.INT

%ignore _S
%ignore _SM
